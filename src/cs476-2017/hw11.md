---
title: CS476 - Homework 11
---

# `leaves` and `inner`

```{ pipe='tee -a tree.maude' }
fmod TREE is
  protecting QID .
  sorts Natural Tree .
  op 0 : -> Natural [ctor].
  op s : Natural -> Natural [ctor].
  op _+_ : Natural Natural -> Natural [assoc comm].

  subsort Qid < Tree .
  op _#_ : Tree Tree -> Tree [ctor] .
  ops leaves inner : Tree -> Natural .

  var I : Qid .
  vars N M : Natural .
  vars T T' : Tree .
  eq N + 0 = N .
  eq N + s(M) = s(N + M) .
  *** add equations for leaves and inner here

  eq inner( I ) = 0 .
  eq inner( T # T') = inner(T) + inner(T') + s(0) .

  eq leaves( I ) = s(0) .
  eq leaves(T # T') = leaves(T) + leaves(T') .
endfm
```

Tests for `inner`:
```{ pipe='maude-test tree.maude' }
reduce inner('a)                     == 0 .
reduce inner('a # 'b)                == s(0) .
reduce inner(('a # 'b) # ('c # 'd))  == s(s(s(0))) .
```
Tests for `leaves`:
```{ pipe='maude-test tree.maude' }
reduce leaves('a)                    == s(0) .
reduce leaves('a # (('b # 'c) # 'd)) == s(s(s(s(0)))) .
```

Check for sufficient completeness:

```{ pipe='maude-scc tree.maude TREE' }
```

We claim that: $\forall t \in \texttt{Tree}, \texttt{leaves}(t) = 1 + \texttt{inner}(t)$
----------------------------------------------------------------------------------------

Below is a proof for the `ITP`:

```{pipe='tee tree-leaves-vs-inner.maude'}
load itp-tool.maude
loop init-itp .
(goal tree-leaves-vs-inner : TREE |- A{T:Tree} ((leaves(T)) = (s(inner(T)))) .)
(ind* on T .)
```

```{ pipe='maude -no-banner tree.maude tree-leaves-vs-inner.maude' }
```

Dining Philosophers
===================

```{ .hidden pipe='cat > din-phil.maude' }
fmod NAT/4 is
   protecting NAT .
   sort Nat/4 .
   op [_] : Nat -> Nat/4 .
   op _+_ : Nat/4 Nat/4 -> Nat/4 .
   op _*_ : Nat/4 Nat/4 -> Nat/4 .
   op p : Nat/4 -> Nat/4 .
   vars N M : Nat .
   ceq [N] = [N rem 4] if N >= 4 .
   eq [N] + [M] = [N + M] .
   eq [N] * [M] = [N * M] .
   ceq p([0]) = [N] if s(N) := 4 .
   ceq p([s(N)]) = [N] if N < 4 .
endfm

mod DIN-PHIL is
   protecting NAT/4 .
   sorts Oid Cid Attribute AttributeSet Configuration Object Msg .
   sorts Phil Mode .
   subsort Nat/4 < Oid .
   subsort Attribute < AttributeSet .
   subsort Object < Configuration .
   subsort Msg < Configuration .
   subsort Phil < Cid .

   op __ : Configuration Configuration -> Configuration
                                                  [ assoc comm id: none ] .
   op _`,_ : AttributeSet AttributeSet -> AttributeSet
                                                  [ assoc comm id: null ] .
   op null : -> AttributeSet .
   op none : -> Configuration .
   op mode`:_ : Mode -> Attribute [ gather ( & ) ] .
   op holds`:_ : Configuration -> Attribute [ gather ( & ) ] .
   op <_:_|_> : Oid Cid AttributeSet -> Object .
   op Phil : -> Phil .

   ops t h e : -> Mode .
   op chop : Nat/4 Nat/4 -> Msg [comm] .
   op init : -> Configuration .
   op make-init : Nat/4 -> Configuration .

   vars N M K : Nat .
   var C : Configuration .

   ceq init = make-init([N]) if s(N) := 4 .
   ceq make-init([s(N)])
     = < [s(N)] : Phil | mode : t , holds : none >  make-init([N])  (chop([s(N)],[N]))
     if N < 4 .
   ceq make-init([0]) =
      < [0] : Phil | mode : t , holds : none > chop([0],[N]) if  s(N) := 4 .

   rl [t2h] : < [N] : Phil | mode : t , holds : none > =>
      < [N] : Phil | mode : h , holds : none > .
   crl [pickl] :  < [N] : Phil | mode : h , holds : none > chop([N],[M])
       => < [N] : Phil | mode : h , holds :  chop([N],[M]) > if [M] = [s(N)] .
   rl [pickr] :  < [N] : Phil | mode : h , holds : chop([N],[M]) >
      chop([N],[K]) =>
      < [N] : Phil | mode : h , holds :  chop([N],[M]) chop([N],[K]) > .
   rl [h2e] :  < [N] : Phil | mode : h , holds :  chop([N],[M])
      chop([N],[K]) > => < [N] : Phil | mode : e ,
      holds :  chop([N],[M]) chop([N],[K]) > .
   rl [e2t] :  < [N] : Phil | mode : e , holds :  chop([N],[M])
      chop([N],[K]) >  =>   chop([N],[M]) chop([N],[K])
      < [N] : Phil | mode : t , holds :  none > .
endm
```

-   Two adjacent philosophers shouldn't be eating simultaneously:

    ``` {pipe="tee din-phil-no-adjacent.maude"}
        search [1] in DIN-PHIL  : init =>*
            <[  N:Nat ] : Phil | mode : e ,  A:AttributeSet >
            <[s(N:Nat)] : Phil | mode : e , A':AttributeSet >
            C:Configuration
          .
    ```

    ``` {pipe="maude -no-banner din-phil.maude din-phil-no-adjacent.maude"}
    ```

-   But two can eat simultaneously:

    ```{pipe="tee din-phil-dinner-for-two.maude"}
        search [1] in DIN-PHIL  : init =>*
            <   [N:Nat] : Phil | mode : e ,  A:AttributeSet >
            <   [M:Nat] : Phil | mode : e , A':AttributeSet >
            C:Configuration
          .
    ```
    ``` {pipe="maude -no-banner din-phil.maude din-phil-dinner-for-two.maude"}
    ``` 

-   and not three:

    ``` {pipe="tee din-phil-three-is-a-crowd.maude | maude -no-banner din-phil.maude"}
        search [1] in DIN-PHIL  : init =>*
            <   [N:Nat] : Phil | mode : e ,  A:AttributeSet >
            <   [M:Nat] : Phil | mode : e , A':AttributeSet >
            <   [O:Nat] : Phil | mode : e , A'':AttributeSet >
            C:Configuration
          .
    ```

    ``` {pipe="maude -no-banner din-phil.maude din-phil-three-is-a-crowd.maude"}
    ```

-   \ldots but there is a deadlock, where all four philosophers are hungry
    but cannot eat:

    ``` {pipe="tee din-phil-deadlock.maude | maude -no-banner din-phil.maude"}
        search [1] in DIN-PHIL  : init =>!
            <   [N:Nat] : Phil | mode : h ,  A:AttributeSet >
            <   [M:Nat] : Phil | mode : h ,  B:AttributeSet >
            <   [O:Nat] : Phil | mode : h ,  C:AttributeSet >
            <   [P:Nat] : Phil | mode : h ,  D:AttributeSet >
            C:Configuration
          .
    ```
    
    ``` {pipe="maude -no-banner din-phil.maude din-phil-deadlock.maude"}
    ```
