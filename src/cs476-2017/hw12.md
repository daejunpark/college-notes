---
title: Homework 12 - CS 476
---

```{.hidden pipe='cat > comm-chann.maude'}
mod COMM-CHANN is protecting NAT .  protecting QID .
    sorts Msg MsgMSet QidList CHState .
    subsort Msg <  MsgMSet .
    subsort Qid < QidList .

    op nil : -> QidList [ctor] . 
    op _;_ : QidList QidList -> QidList [ctor assoc id: nil] .
    op [_,_] : Qid Nat -> Msg [ctor] .
    op null : -> MsgMSet [ctor] .
    op _ _ :  MsgMSet MsgMSet -> MsgMSet [ctor assoc comm id: null] .
    op [_:_|_|_:_] : QidList Nat MsgMSet Nat QidList -> CHState [ctor] .

    vars N M I J : Nat . var MS : MsgMSet . vars L L' : QidList .
    vars A B : Qid .

    rl [send] : [A ; L : I | MS | J : L'] => [L : s(I) | [A,I] MS | J : L'] .
    rl [receive] : [L : I | MS [A,J] | J : L'] => [L : I | MS | s(J) : L' ; A] .
endm
```

We define the `in-order-reception` using matching to check that the recieved
message is a prefix of the sent message:

``` {pipe='tee question-one.maude'}
mod Q1 is
    protecting COMM-CHANN .
    sort State .
    subsort CHState < State .
    
    vars S : CHState .
    vars L L' L1 : QidList .
    vars N M : Nat .
    vars MS : MsgMSet .
    op in-order-reception : State QidList -> Bool .
    eq in-order-reception( [ L1 : N | MS | M : L ]
                         , L ; L'
                         ) = true .
    eq in-order-reception( S, L) = false [owise] .
    
    op init : QidList -> State .
    eq init(L) = [ L : 0 | null | 0 : nil ] .
endm

```

```{pipe='maude -no-banner comm-chann.maude question-one.maude' }
    search in Q1 : init('a ; 'b ; 'c)
       =>* S:CHState
      such that in-order-reception(S, 'a ; 'b ; 'c) = false .
      
    search in Q1 : init('a ; 'b ; 'c ; 'd)
       =>* S:CHState
      such that in-order-reception(S, 'a ; 'b ; 'c ; 'd ) = false .
      
    search in Q1 : init('a ; 'b ; 'c ; 'd ; 'e)
       =>* S:CHState
      such that in-order-reception(S, 'a ; 'b ; 'c ; 'd ; 'e) = false .
```


# LTL Checker

```{.hidden pipe='cat > comm-chann-preds.maude'}
load model-checker.maude

mod COMM-CHANN-PREDS is
    protecting COMM-CHANN .
    including SATISFACTION .

    subsort CHState < State .
    op in-order-reception : QidList -> Prop .
    op received : QidList -> Prop .

    vars N M I J : Nat . var MS : MsgMSet . vars L L' L'' : QidList .
    *** add your defining equations here


```

We add the equations for `in-order-reception`:

```{pipe='tee --append comm-chann-preds.maude'}
    eq [ L'' : N | MS | M : L ] |= in-order-reception(L ; L')
     = true .
    eq S:State |= in-order-reception(L)
     = false [owise] .
```

and define the  `recieved` predicate:

```{pipe='tee --append comm-chann-preds.maude'}

    op received : QidList -> Prop .
    eq [ nil : N | MS | M : L ] |= received(L)
     = true .
    eq S:State |= received(L)
     = false [owise] .
```

```{.hidden pipe='cat >> comm-chann-preds.maude'}

endm
```

```{pipe='cat > comm-chann-check.maude'}
mod COMM-CHANN-CHECK is
    protecting COMM-CHANN-PREDS .
    including MODEL-CHECKER .

    ops init1 init2 init3  : -> CHState .
    eq init1 = ['a ; 'b ; 'c : 0 | null | 0 : nil ] .
    eq init2 = ['a ; 'b ; 'c ; 'd : 0 | null | 0 : nil ] .
    eq init3 = ['a ; 'b ; 'c ; 'd ; 'e : 0 | null | 0 : nil ] .
endm

*** add your LTL comands here
```

Now, we use the LTL Model checker to check that the `in-order-reception` invariant holds:

```{pipe='maude-test comm-chann.maude comm-chann-preds.maude  comm-chann-check.maude' }
reduce modelCheck(init1, [] in-order-reception('a ; 'b ; 'c)) .
reduce modelCheck(init2, [] in-order-reception('a ; 'b ; 'c ; 'd)) .
reduce modelCheck(init3, [] in-order-reception('a ; 'b ; 'c ; 'd ; 'e)) .
``` 

and do the same for the `received` invariant:

```{pipe='maude-test comm-chann.maude comm-chann-preds.maude  comm-chann-check.maude' }
reduce modelCheck(init1, <> received('a ; 'b ; 'c)) .
reduce modelCheck(init2, <> received('a ; 'b ; 'c ; 'd)) .
reduce modelCheck(init3, <> received('a ; 'b ; 'c ; 'd ; 'e)) .
``` 
